vertex_attribute_container vertex
{
    f3 position;
    f2 uv;
};

instanced_attribute_container instanced
{
    f4x4 model;
    u1 preset_index;
    u1 grid_index;
};

struct preset_t
{
    f4 modifier_1;
    f4 modifier_2;
};

set_material read_only_storage_buffer material
{
    preset_t... presets;
};

struct grid_t
{
    f1 thickness;
    f1 separator;
};

set_material read_only_storage_buffer grid
{
    grid_t... grids;
};

state_container state
{
    f2 uv;
    u1 preset_index;
    u1 grid_index;
};

void vertex_main (void)
{
    state.uv = vertex.uv;
    state.preset_index = instanced.preset_index;
    state.grid_index = instanced.grid_index;
    vertex_stage_output_position (pass.projection_view * instanced.model * f4 {vertex.position, 1.0});
}

set_material sampler texture_sampler;
set_material image_color_2d texture_1_color;
set_material image_color_2d texture_2_color;

color_output_container fragment_output
{
    f4 color;
};

f1 calculate_relative_distance_to_grid (in f2 uv)
{
    f2 separator_vector = f2 {grid.grids[state.grid_index].separator};
    f2 localized_fracts = fract_f2 (uv / separator_vector);
    f2 distances = abs_f2 (localized_fracts - step_f2 (f2 {0.5}, localized_fracts));
    return min_f1 (distances.x, distances.y);
}

void fragment_main (void)
{
    f1 relative_distance_from_grid = calculate_relative_distance_to_grid (state.uv);
    f1 relative_half_thickness =
        0.5 * grid.grids[state.grid_index].thickness /
        grid.grids[state.grid_index].separator;

    if (relative_distance_from_grid < relative_half_thickness ||
        relative_distance_from_grid > 1.0 - relative_half_thickness)
    {
        fragment_output.color = f4 {0.0, 0.0, 0.0, 1.0};
    }
    else
    {
        f4 color_1 = sample (texture_sampler, texture_1_color, state.uv);
        f4 color_2 = sample (texture_sampler, texture_2_color, state.uv);
        fragment_output.color =
            color_1 * material.presets[state.preset_index].modifier_1 +
            color_2 * material.presets[state.preset_index].modifier_2;
    }
}
